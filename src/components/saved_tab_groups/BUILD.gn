# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")
}

source_set("core") {
  sources = [
    "empty_tab_group_store_delegate.cc",
    "empty_tab_group_store_delegate.h",
    "stats.cc",
    "stats.h",
    "tab_group_store.cc",
    "tab_group_store.h",
    "tab_group_store_delegate.h",
    "tab_group_store_id.cc",
    "tab_group_store_id.h",
    "tab_group_sync_service.h",
    "tab_group_sync_service_impl.cc",
    "tab_group_sync_service_impl.h",
  ]
  deps = [
    ":saved_tab_group_bridge",
    ":shared_tab_group_bridge",
    "//base",
    "//components/keyed_service/core",
    "//components/sync",
    "//components/tab_groups",
    "//ui/gfx",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/tab_group_store_delegate_android.cc",
      "android/tab_group_store_delegate_android.h",
      "android/tab_group_sync_conversions_bridge.cc",
      "android/tab_group_sync_conversions_bridge.h",
      "android/tab_group_sync_conversions_utils.cc",
      "android/tab_group_sync_conversions_utils.h",
      "android/tab_group_sync_service_android.cc",
      "android/tab_group_sync_service_android.h",
    ]

    deps += [ ":jni_headers" ]
  }

  public_deps = [ ":model" ]
}

source_set("model") {
  sources = [
    "features.cc",
    "features.h",
    "pref_names.cc",
    "pref_names.h",
    "saved_tab_group.cc",
    "saved_tab_group.h",
    "saved_tab_group_model.cc",
    "saved_tab_group_model.h",
    "saved_tab_group_model_observer.h",
    "saved_tab_group_tab.cc",
    "saved_tab_group_tab.h",
    "types.h",
  ]
  deps = [
    "//base",
    "//components/pref_registry",
    "//components/sync",
    "//components/tab_groups",
    "//ui/gfx",
    "//url",
  ]
  public_deps = []
}

if (is_android) {
  android_library("java") {
    sources = [
      "android/java/src/org/chromium/components/tab_group_sync/LocalTabGroupId.java",
      "android/java/src/org/chromium/components/tab_group_sync/SavedTabGroup.java",
      "android/java/src/org/chromium/components/tab_group_sync/SavedTabGroupTab.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupMetadataPersistentStore.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupStoreDelegate.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncConversionsBridge.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncService.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncServiceImpl.java",
    ]

    deps = [
      ":tab_group_id_metadata_proto_java",
      "//base:base_java",
      "//components/tab_groups:tab_groups_java",
      "//net/android:net_java",
      "//third_party/android_deps:protobuf_lite_runtime_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/jni_zero:jni_zero_java",
      "//url:gurl_java",
    ]

    srcjar_deps = [
      ":jni_headers",
      ":tab_group_sync_enums_java",
    ]
  }

  generate_jni("jni_headers") {
    visibility = [
      ":*",
      "//chrome/browser",
    ]

    sources = [
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupStoreDelegate.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncConversionsBridge.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncServiceImpl.java",
    ]
  }

  robolectric_library("components_tab_group_sync_junit_tests") {
    include_android_sdk = false
    sources = [ "android/java/src/org/chromium/components/tab_group_sync/TabGroupMetadataPersistentStoreUnitTest.java" ]

    deps = [
      ":java",
      ":tab_group_id_metadata_proto_java",
      "//base:base_java",
      "//base:base_java_test_support",
      "//base:base_junit_test_support",
      "//third_party/android_deps:protobuf_lite_runtime_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/junit",
    ]
  }

  java_cpp_enum("tab_group_sync_enums_java") {
    visibility = [ ":*" ]
    sources = [ "tab_group_sync_service.h" ]
  }

  android_library("native_java_unittests_java") {
    testonly = true
    deps = [
      ":java",
      "//base:base_java",
      "//base:base_java_test_support",
      "//components/tab_groups:tab_groups_java",
      "//third_party/jni_zero:jni_zero_java",
      "//third_party/junit",
      "//third_party/mockito:mockito_java",
      "//url:url_java",
    ]

    srcjar_deps = [ ":native_j_unittests_jni_headers" ]
    sources = [
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupStoreDelegateTestSupport.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncServiceAndroidUnitTest.java",
    ]
  }

  generate_jni("native_j_unittests_jni_headers") {
    testonly = true
    sources = [
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupStoreDelegateTestSupport.java",
      "android/java/src/org/chromium/components/tab_group_sync/TabGroupSyncServiceAndroidUnitTest.java",
    ]
  }

  proto_java_library("tab_group_id_metadata_proto_java") {
    proto_path = "android/java/src/org/chromium/components/tab_group_sync/proto"
    sources = [ "$proto_path/tab_group_id_metadata.proto" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "saved_tab_group_model_unittest.cc",
    "saved_tab_group_proto_conversion_unittest.cc",
    "saved_tab_group_test_utils.cc",
    "saved_tab_group_test_utils.h",
    "saved_tab_group_unittest.cc",
    "tab_group_store_unittest.cc",
    "tab_group_sync_service_unittest.cc",
  ]
  deps = [
    ":core",
    ":saved_tab_group_bridge_unittests",
    ":shared_tab_group_bridge_unittests",
    "//base",
    "//base/test:test_support",
    "//components/sync",
    "//components/sync:test_support",
    "//components/tab_groups",
    "//testing/gtest",
    "//url",
  ]

  if (is_android) {
    sources += [
      "android/tab_group_store_delegate_android_unittest.cc",
      "android/tab_group_sync_service_android_unittest.cc",
    ]
    deps += [
      ":native_j_unittests_jni_headers",
      ":native_java_unittests_java",
    ]
  }
}

source_set("saved_tab_group_bridge") {
  visibility = [
    ":*",

    # This is allowed until usage of SavedTabGroupKeyedService has been migrated
    # to TabGroupSyncService.
    "//chrome/browser/ui",
  ]

  sources = [
    "saved_tab_group_sync_bridge.cc",
    "saved_tab_group_sync_bridge.h",
  ]

  deps = [ ":model" ]

  public_deps = [
    "//base",
    "//components/sync/base",
    "//components/sync/model",
  ]
}

source_set("saved_tab_group_bridge_unittests") {
  testonly = true

  visibility = [ ":unit_tests" ]

  sources = [ "saved_tab_group_sync_bridge_unittest.cc" ]

  deps = [
    ":model",
    ":saved_tab_group_bridge",
    "//base",
    "//base/test:test_support",
    "//components/sync",
    "//components/sync:test_support",
    "//components/tab_groups",
    "//testing/gtest",
    "//url",
  ]
}

source_set("shared_tab_group_bridge") {
  visibility = [ ":*" ]

  sources = [
    "shared_tab_group_data_sync_bridge.cc",
    "shared_tab_group_data_sync_bridge.h",
  ]

  deps = [ ":model" ]

  public_deps = [
    "//base",
    "//components/sync/base",
    "//components/sync/model",
  ]
}

source_set("shared_tab_group_bridge_unittests") {
  testonly = true

  visibility = [ ":unit_tests" ]

  sources = [ "shared_tab_group_data_sync_bridge_unittest.cc" ]

  deps = [
    ":shared_tab_group_bridge",
    "//base",
    "//base/test:test_support",
    "//components/sync",
    "//components/sync:test_support",
    "//testing/gtest",
  ]
}
