# Copyright (c) 2022 The Wootz Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/. */

import("//build/buildflag_header.gni")
import("//build/util/branding.gni")
import("//mojo/public/tools/bindings/mojom.gni")

declare_args() {
  wootz_services_key = ""
}

if (is_official_build) {
  assert(wootz_services_key != "")
}

buildflag_header("wootz_services_key") {
  # Please use //components/constants instead.
  visibility = [ ":*" ]

  header = "wootz_services_key.h"
  flags = [ "WOOTZ_SERVICES_KEY=\"$wootz_services_key\"" ]
}

source_set("constants") {
  sources = [
    "wootz_constants.cc",
    "wootz_constants.h",
    "wootz_paths.h",
    "wootz_switches.h",
    "network_constants.h",
    "pref_names.h",
    "url_constants.h",
    "webui_url_constants.h",
  ]

  public_deps = [ ":wootz_services_key" ]
  deps = [ "//base" ]
}

source_set("wootz_service_key_helper") {
  sources = [
    "wootz_services_key_helper.cc",
    "wootz_services_key_helper.h",
  ]

  deps = [
    ":constants",
    "//chrome/extensions:common",  # instead of //extensions/common, for iOS
    "//url",
  ]
}
