# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generates an output file that contains all histograms which share the same
# variant name. This output file will act as an allow list.
#
# Parameters:
#   input_xml_file: Input file that contains the allow list. This should be a
#     .xml file with histogram descriptions and should be a path starting with
#     //tools/metrics/histograms/
#
#   namespace:
#     Namespace in which the generated code should be scoped.
#
#   output_file:
#     Name of the generated file to be used for compile time checking.
#
#   allow_list_name:
#     Name of the variants list that act as an allow list.
#
template("generate_histograms_variants_allowlist") {
  action(target_name) {
    output_file = "$target_gen_dir/" + invoker.output_file

    script =
        "//tools/metrics/histograms/generate_histograms_variants_allowlist.py"
    outputs = [ output_file ]
    sources = [ invoker.input_xml_file ]

    args = [
      "-a" + invoker.allow_list_name,
      "-n" + invoker.namespace,
      "-o" + rebase_path(root_gen_dir, root_build_dir),
      "-f" + rebase_path(output_file, root_gen_dir),
      "-i" + rebase_path(invoker.input_xml_file, root_build_dir),
    ]
  }
}
