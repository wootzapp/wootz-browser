# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

# For this component iOS is explicitly not supported.
if (use_blink) {
  source_set("browser") {
    sources = [
      "fingerprinting_protection_child_navigation_throttle.cc",
      "fingerprinting_protection_child_navigation_throttle.h",
      "fingerprinting_protection_filter_constants.cc",
      "fingerprinting_protection_filter_constants.h",
      "fingerprinting_protection_observer.h",
      "fingerprinting_protection_page_activation_throttle.cc",
      "fingerprinting_protection_page_activation_throttle.h",
      "fingerprinting_protection_profile_interaction_manager.cc",
      "fingerprinting_protection_profile_interaction_manager.h",
      "fingerprinting_protection_web_contents_helper.cc",
      "fingerprinting_protection_web_contents_helper.h",
    ]
    public_deps = [
      ":features",
      "//base",
      "//components/content_settings/core/browser:cookie_settings",
      "//components/content_settings/core/common:common",
      "//components/privacy_sandbox:tracking_protection_settings",
      "//components/subresource_filter/content/shared/browser",
      "//components/subresource_filter/content/shared/common",
      "//content/public/browser",
    ]
    deps = [
      "//components/content_settings/browser",
      "//components/subresource_filter/core/browser",
      "//components/subresource_filter/core/common",
      "//components/subresource_filter/core/mojom",
      "//url",
    ]
  }

  component("features") {
    output_name = "fingerprinting_protection_filter_features"
    defines = [ "IS_FINGERPRINTING_PROTECTION_FILTER_FEATURES_IMPL" ]
    sources = [
      "fingerprinting_protection_filter_features.cc",
      "fingerprinting_protection_filter_features.h",
    ]
    public_deps = [ "//base" ]
    deps = [ "//components/subresource_filter/core/mojom" ]
  }

  source_set("unit_tests") {
    testonly = true
    sources = [
      "fingerprinting_protection_child_navigation_throttle_unittest.cc",
      "fingerprinting_protection_page_activation_throttle_unittest.cc",
      "fingerprinting_protection_profile_interaction_manager_unittest.cc",
    ]
    deps = [
      ":browser",
      "//base",
      "//components/privacy_sandbox:features",
      "//components/subresource_filter/content/shared/browser:test_support",
      "//components/subresource_filter/core/common",
      "//components/subresource_filter/core/mojom",
      "//components/sync_preferences:test_support",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
      "//url",
    ]
  }
}
