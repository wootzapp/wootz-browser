# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

source_set("webapk") {
  if (is_chromeos) {
    sources = [
      "webapk_utils.cc",
      "webapk_utils.h",
    ]

    configs += [ "//build/config/compiler:wexit_time_destructors" ]

    deps = [
      "//base",
      "//chrome/browser/profiles",
      "//chrome/browser/web_applications",
      "//chromeos/crosapi/mojom",
      "//components/services/app_service",
      "//components/webapk:proto",
      "//third_party/blink/public/mojom:mojom_platform_headers",
      "//third_party/smhasher:murmurhash2",
      "//url",
    ]
  }

  if (is_chromeos_ash) {
    sources += [
      "webapk_install_queue.cc",
      "webapk_install_queue.h",
      "webapk_install_task.cc",
      "webapk_install_task.h",
      "webapk_metrics.cc",
      "webapk_metrics.h",
    ]

    deps += [
      ":prefs",
      "//ash",
      "//ash/components/arc:arc_base",
      "//ash/components/arc/mojom",
      "//ash/components/arc/session",
      "//ash/components/arc/session:connection_holder",
      "//chrome/browser/ash/crosapi",
      "//chrome/browser/profiles",
      "//chrome/browser/profiles:profile",
      "//chrome/common:non_code_constants",
      "//chromeos/crosapi/mojom",
      "//components/version_info",
      "//content/public/browser",
      "//net",
      "//services/network/public/cpp",
      "//services/network/public/mojom",
    ]
  }
}

source_set("prefs") {
  sources = [
    "webapk_prefs.cc",
    "webapk_prefs.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//base",
    "//chrome/browser/profiles",
    "//components/prefs",
  ]
}

source_set("unit_tests") {
  testonly = true

  if (is_chromeos_ash) {
    sources = [ "webapk_install_task_unittest.cc" ]

    deps = [
      ":prefs",
      ":test_support",
      ":webapk",
      "//ash/components/arc:arc_test_support",
      "//ash/components/arc/mojom",
      "//ash/components/arc/session",
      "//ash/components/arc/session:connection_holder",
      "//base",
      "//base/test:test_support",
      "//chrome/browser/apps/app_service:test_support",
      "//chrome/browser/ash:test_support",
      "//chrome/browser/web_applications",
      "//chrome/browser/web_applications:web_applications_test_support",
      "//chrome/common:chrome_features",
      "//chrome/test:test_support",
      "//components/prefs",
      "//components/webapk:proto",
      "//content/test:test_support",
      "//net:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

source_set("test_support") {
  testonly = true

  if (is_chromeos_ash) {
    sources = [
      "webapk_test_server.cc",
      "webapk_test_server.h",
    ]

    deps = [
      "//base",
      "//chrome/common:chrome_features",
      "//chrome/common:non_code_constants",
      "//components/webapk:proto",
      "//net:test_support",
    ]
  }
}
