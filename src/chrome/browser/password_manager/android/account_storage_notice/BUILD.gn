# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD - style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

# Public API.
static_library("account_storage_notice") {
  public = [ "account_storage_notice.h" ]
  sources = [
    "account_storage_notice.cc",
    "account_storage_notice.h",
    "coordinator_observer.h",
  ]
  deps = [
    ":jni",
    "//base",
    "//chrome/android:chrome_jni_headers",
    "//components/prefs",
    "//components/sync/base",
    "//components/sync/service",
    "//content/public/browser",
    "//ui/android",
    "//ui/gfx:native_widget_types",
  ]
}

# This target cannot be private because Java must be hooked separately from
# the native library [1]. But it is *effectively* private: all classes are
# package-private and only used by account_storage_notice.cc.
#
# [1] https://source.chromium.org/chromium/chromium/src/+/main:chrome/android/BUILD.gn;l=3932;drc=29634f0b343c5874147170bb4b5d06d17e0be57b
android_library("java") {
  sources = [
    "java/src/org/chromium/chrome/browser/password_manager/account_storage_notice/AccountStorageNoticeCoordinator.java",
    "java/src/org/chromium/chrome/browser/password_manager/account_storage_notice/AccountStorageNoticeView.java",
  ]
  deps = [
    ":resources",
    "//base:base_java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//components/browser_ui/bottomsheet/android:java",
    "//components/browser_ui/settings/android:java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
  ]
  srcjar_deps = [ ":jni" ]
  visibility = [
    ":*",
    "//chrome/browser/password_manager/android:java",
  ]
  resources_package =
      "org.chromium.chrome.browser.password_manager.account_storage_notice"
}

# Private.
android_resources("resources") {
  sources = [
    "java/res/drawable-night/account_storage_notice_illustration.xml",
    "java/res/drawable/account_storage_notice_illustration.xml",
    "java/res/layout/account_storage_notice_layout.xml",
    "java/res/values/dimens.xml",
  ]
  visibility = [ ":*" ]
}

# Private.
generate_jni("jni") {
  sources = [ "java/src/org/chromium/chrome/browser/password_manager/account_storage_notice/AccountStorageNoticeCoordinator.java" ]
  visibility = [ ":*" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "account_storage_notice_unittest.cc" ]
  deps = [
    ":account_storage_notice",
    "//base/test:test_support",
    "//components/password_manager/core/common",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/sync:test_support",
    "//components/sync/base",
    "//testing/gtest",
  ]
}

android_library("javatests") {
  testonly = true
  sources = [ "javatests/src/org/chromium/chrome/browser/password_manager/account_storage_notice/AccountStorageNoticeCoordinatorTest.java" ]
  deps = [
    ":java",
    "//base:base_java_test_support",
    "//chrome/android:chrome_java",
    "//chrome/browser/flags:java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//chrome/test/android:chrome_java_integration_test_support",
    "//components/browser_ui/bottomsheet/android:java",
    "//components/browser_ui/settings/android:java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:espresso_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/hamcrest:hamcrest_core_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_java",
  ]
  resources_package =
      "org.chromium.chrome.browser.password_manager.account_storage_notice"
}
