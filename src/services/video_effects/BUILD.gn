# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//services/video_effects/args.gni")

source_set("service") {
  public = [
    "video_effects_processor_impl.h",
    "video_effects_service_impl.h",
  ]

  sources = [
    "video_effects_processor_impl.cc",
    "video_effects_service_impl.cc",
  ]

  visibility = [
    "//content/utility:utility",
    "//services/video_effects:tests",
  ]

  public_deps = [
    "//services/video_effects/public/mojom:mojom",
    "//services/viz/public/cpp/gpu:gpu",
  ]
}

buildflag_header("buildflags") {
  header = "buildflags.h"

  header_dir = "services/video_effects/public/cpp"

  flags = [ "ENABLE_VIDEO_EFFECTS=$enable_video_effects" ]
}

source_set("tests") {
  testonly = true

  sources = [
    "video_effects_processor_impl_unittest.cc",
    "video_effects_service_impl_unittest.cc",
  ]

  deps = [
    "//base/test:test_support",
    "//media/capture/mojom:video_effects_manager",
    "//services/video_effects:service",
    "//testing/gtest",
  ]
}
